Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Size(15285,10695)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form..Caption("Reservation History")
Gui.Form.gsHist.Create(GsGridControl)
Gui.Form.gsHist.Size(14940,9030)
Gui.Form.gsHist.Position(105,1065)
Gui.Form.lbl1.Create(Label,"Order",True,1140,255,0,135,285,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtSO.Create(TextBox,"",True,1215,300,0,135,525,True,0,"Arial",8,-2147483643,2)
Gui.Form.lbl2.Create(Label,"Line",True,570,255,0,1455,285,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtLine.Create(TextBox,"",True,450,300,0,1500,525,True,0,"Arial",8,-2147483643,2)
Gui.Form.cmdLoad.Create(Button)
Gui.Form.cmdLoad.Size(855,375)
Gui.Form.cmdLoad.Position(3075,480)
Gui.Form.cmdLoad.Caption("Load")
Gui.Form.cmdLoad.Event(Click,cmdLoad_Click)
Gui.Form.optHistory.Create(Option)
Gui.Form.optHistory.Size(1575,255)
Gui.Form.optHistory.Position(5895,555)
Gui.Form.optHistory.Caption("History")
Gui.Form.optActive.Create(Option)
Gui.Form.optActive.Size(1575,255)
Gui.Form.optActive.Position(4305,555)
Gui.Form.optActive.Caption("Current")
Gui.Form.cmdBrowseSO.Create(Button)
Gui.Form.cmdBrowseSO.Size(705,375)
Gui.Form.cmdBrowseSO.Position(2070,480)
Gui.Form.cmdBrowseSO.Caption("^")
Gui.Form.cmdBrowseSO.Event(Click,cmdBrowseSO_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
f.Intrinsic.Control.Try
	V.Local.sError.Declare

	f.ODBC.Connection!conx.OpenCompanyConnection
	gui.Form.gsHist.anchor(15)
	gui.Form.optHistory.Value(True)
	gui.Form..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry

Program.Sub.Main.End

Program.Sub.cmdLoad_Click.Start
f.Intrinsic.Control.Try
	V.Local.sError.Declare
	v.Local.sSO.Declare
	v.Local.sLine.Declare
	v.Local.sSQL.Declare
	v.Local.sLineClause.Declare
	
	
	f.Intrinsic.Control.If(v.DataTable.Hist.Exists)
		f.Data.DataTable.Close("Hist")
	f.Intrinsic.Control.EndIf
	v.Local.sSO.Set(v.Screen.Form!txtSO.Text.Trim)
	v.Local.sLine.Set(v.Screen.Form!txtLine.Text.Trim)
	
	f.Intrinsic.Control.If(v.Local.sLine.Length,=,3)
		f.Intrinsic.String.build("{0}0",v.Local.sLine,v.Local.sLine)
		f.Intrinsic.String.Build("and Line = '{0}'",v.Local.sLine,v.Local.sLineClause)
	f.Intrinsic.Control.ElseIf(v.Local.sLine.Length,=,4)
		f.Intrinsic.String.Build("and Line = '{0}'",v.Local.sLine,v.Local.sLineClause)
	f.Intrinsic.Control.Else
		v.Local.sLineClause.Set("")
	f.Intrinsic.Control.EndIf
	
	
	
	f.Intrinsic.Control.If(v.Screen.Form!optHistory.Value,=,True)
		f.Intrinsic.String.Build("Select SO, left(Line,3) as Line,Part,Loc,Current_Reserved,Qty_Reserved,View_User,Date_Reserved from GCG_5210_hist where SO = '{0}' {1} order by SO,Line,date_reserved ",v.Local.sSO,v.Local.sLineClause,v.Local.sSQL)
	f.Intrinsic.Control.Else
		f.Intrinsic.String.Build("Select SO, left(Line,3) as Line,Part,Loc,Current_Reserved,Qty_Reserved,View_User,Date_Reserved from GCG_5210_Reserve where SO = '{0}' {1} order by SO,Line,date_reserved ",v.Local.sSO,v.Local.sLineClause,v.Local.sSQL)
	f.Intrinsic.Control.EndIf

	f.data.DataTable.CreateFromSQL("Hist","conx",v.Local.sSQL,True)		
		
	
	gui.Form.gsHist.AddGridviewFromDatatable("GVHist","Hist")
	f.Intrinsic.Control.CallSub("Format_Grid")
	gui.Form.gsHist.MainView("GVHist")
	


F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry

Program.Sub.cmdLoad_Click.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.CallSub("Unload")
Program.Sub.Form_UnLoad.End

Program.Sub.UnLoad.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare

	F.Intrinsic.Control.If(V.ODBC.conx.State,=,1)
		F.ODBC.Connection!conx.close
	F.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	Function.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End	
F.Intrinsic.Control.EndTry
Program.Sub.UnLoad.End

Program.Sub.Format_Grid.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sLableTotal.Declare
	
	Gui.Form.gsHist.SuspendLayout()
	'General format for the grid
	Gui.Form.gsHist.SetGridViewProperty("GVHist","AllowSort",True)
	Gui.Form.gsHist.SetGridViewProperty("GVHist","AllowFilter",True)
	Gui.Form.gsHist.SetGridViewProperty("GVHist","OptionsFindAlwaysVisible",True)
	Gui.Form.gsHist.SetGridViewProperty("GVHist","EnableAppearanceOddRow",False)
	Gui.Form.gsHist.SetGridViewProperty("GVHist","OptionsViewShowAutoFilterRow",True)
	'Visible Index
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","SO,Line,Part,Loc,Current_Reserved,Qty_Reserved,View_User,Date_Reserved","sProperty","VisibleIndex")
	'Invisible
'	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","","sProperty","Invisible")
	''Width	
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","Loc,Line","sProperty","Width","sWidth","40")
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","SO","sProperty","Width","sWidth","80")
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","Current_Reserved,Date_Reserved","sProperty","Width","sWidth","130")
	'Caption
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","SO@!@Order,Current_Reserved@!@Current Reserved Qty,Qty_Reserved@!@Transaction Qty,View_User@!@User,Date_Reserved@!@Date Reserved","sProperty","Caption")		
	'Header Font bold
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","SO,Line,Part,Loc,Current_Reserved,Qty_Reserved,View_User,Date_Reserved","sProperty","HeaderFontBold")
	'HeaderAllign_Far
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","Current_Reserved,Qty_Reserved","sProperty","HeaderAllign_Far")
	
	'Format Display Numeric
	f.Intrinsic.Control.CallSub(FormatBulk,"GS","gsHist","GV","GVHist","sInputColumns","Current_Reserved,Qty_Reserved","sProperty","Decimal_1")	

	gui.Form.gsHist.ResumeLayout()

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)	
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Format_Grid.End

Program.Sub.FormatBulk.Start
f.Intrinsic.Control.Try
	V.Local..BulkDeclareString(sError,sColumns,sHold)
	v.Local..BulkDeclareLong(iCnt)
	
	f.Intrinsic.String.Split(v.Args.sInputColumns,",",v.Local.sColumns)
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.Local.sColumns.UBound,1)
		F.Intrinsic.Control.SelectCase(v.Args.sProperty)
			F.Intrinsic.Control.Case("MinWidth")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"MinWidth",v.Args.sWidth)
			F.Intrinsic.Control.Case("MaxWidth")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"MaxWidth",v.Args.sWidth)
			F.Intrinsic.Control.Case("Width")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"Width",v.Args.sWidth)
			F.Intrinsic.Control.Case("Invisible")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"Visible",False)
			F.Intrinsic.Control.Case("VisibleIndex")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"VisibleIndex",v.Local.iCnt)
			F.Intrinsic.Control.Case("Caption")
				f.Intrinsic.String.Split(v.Local.sColumns(v.Local.iCnt),"@!@",v.Local.sHold)
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sHold(0),"Caption",v.Local.sHold(1))
			F.Intrinsic.Control.Case("HeaderAllign_Far")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"HeaderHAlignment","Far")
			F.Intrinsic.Control.Case("HeaderFontBold")	
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"HeaderFontBold",True)
			F.Intrinsic.Control.Case("Decimal_1")	
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"DisplayCustomNumeric","###,###,###,##0.00")
			f.Intrinsic.Control.Case("CellBackColor")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"CellBackColor",v.Args.sColor)
			f.Intrinsic.Control.Case("For_CellValue_Changed")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"AllowEdit",True)	
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"ReadOnly",False)
			f.Intrinsic.Control.Case("For_RowCell_Click")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"AllowEdit",False)	
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"ReadOnly",True)
			f.Intrinsic.Control.Case("FixedColumn_Left")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"Fixed","Left")
			f.Intrinsic.Control.Case("Link")
				gui.Form.[v.Args.GS].SetColumnProperty(v.Args.GV,v.Local.sColumns(v.Local.iCnt),"CellForeColor","Blue")
			f.Intrinsic.Control.Case("SummaryItem")
				f.Intrinsic.String.Build("Sum_{0}",v.Local.sColumns(v.Local.iCnt),v.Local.sHold)
				gui.Form.[v.Args.GS].AddSummaryItem(v.Args.GV,v.Local.sColumns(v.Local.iCnt),v.Local.sHold,"Sum","","","n")
			F.Intrinsic.Control.EndSelect		
	f.Intrinsic.Control.Next(v.Local.iCnt)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.FormatBulk.End

Program.Sub.cmdBrowseSO_Click.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sRet.Declare
	
	F.Intrinsic.UI.SetBrowserHotTypeAhead(true)
	F.Intrinsic.UI.Browser("Sales Order","conx","Select Order_No,left(RECORD_NO,3) as Line,Customer,Qty_Ordered,Qty_Shipped,Part,Price from V_Order_Lines order by Order_No, Line","Order_No*!*Line*!*Customer*!*Qty_Ordered*!*Qty_Shipped*!*Part*!*Price","200*!*200*!*500*!*300*!*300*!*300*!*300",Variable.Local.sRet)
	
	'Check if you choose any record from the result
	F.Intrinsic.Control.If(Variable.Local.sRet.IsCancel)
		F.Intrinsic.UI.Msgbox("You select NOTHING!")
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Split(Variable.Local.sRet,"*!*",Variable.Local.sRet)
		gui.Form.txtSO.Text(v.Local.sRet(0).Trim)		
	F.Intrinsic.Control.EndIf
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Current Subroutine: {2}{1}Subroutine Called From: {3}{1}Error Occurred {4}{1}Error description: {5}{1}GAB Version: {6}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,v.Ambient.SubroutineCalledFrom,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseSO_Click.End
